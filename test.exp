#!/usr/bin/env expect

log_file test.log

if {$argc ne 1} {
  puts "Must provide only the path to a tackle executable."
  exit 1
}

set tackle [lindex $argv 0]

# begin with a clean slate
set preinstalled false
spawn ./$tackle list tutstack
expect {
    "tutstack" {
        set preinstalled true
        exec ./$tackle remove tutstack
    }
    "Error" {
        puts "Could not setup clean test enviornment"
        exit 1
    }
    "\n" {
        set preinstalled false
    }
}

spawn ./$tackle list tutstack
expect {
    "tutstack" {dict set results list false}
    "Error"    {dict set results list false}
    "\n"       {dict set results list true}
}


spawn ./$tackle search tutstack
expect {
    "An example Tcl package" {dict set results search true}
    "Error"                  {dict set results search false}
    "\n"                     {dict set results search false}
}

spawn ./$tackle add tutstack
expect {
    "Added package tutstack"        {dict set results add true}
    "tutstack is already installed" {dict set results add false}
    "Error"                         {dict set results add false}
    "\n"                            {dict set results add false}
}

spawn ./$tackle show tutstack
expect {
    "An example Tcl package" {dict set results show true}
    "Error"                  {dict set results show false}
    "\n"                     {dict set results show false}
}

spawn ./$tackle remove tutstack
expect {
    "Removed tutstack"          {dict set results remove true}
    "tutstack is not installed" {dict set results remove false}
    "Error"                     {dict set results remove false}
    "\n"                        {dict set results remove false}
}

if $preinstalled {
    exec ./$tackle add tutstack
}

set passed 0
set failed 0

foreach {command result} $results {
    if $result {
        append verbose "✅ $command\n"
        incr passed
    } else {
        append verbose "❌ $command\n"
        incr failed
    }
}

if {$failed ne 0} {
    puts "❌ $failed tests failed."
    puts $verbose
    exit 1
} else {
    puts "✅ All $passed tests passed."
}

